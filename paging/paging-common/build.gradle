/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.Publish
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootExtension
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("org.jetbrains.kotlin.multiplatform")
}

kotlin {
    jvm {
        withJava()
    }
    js {
        nodejs()
    }

    sourceSets {
        commonMain {
            dependencies {
                api(libs.kotlinStdlibCommon)
                api(libs.kotlinCoroutinesCore)
                implementation("co.touchlab:stately-concurrency:1.2.0")
                implementation("co.touchlab:stately-iso-collections:1.2.0")
            }
        }
        commonTest {
            dependencies {
                implementation(libs.kotlinTestCommon)
                implementation(libs.kotlinTestAnnotationsCommon)
                implementation(libs.kotlinCoroutinesTest)
            }
        }
        jsMain {
            dependencies {
            }
        }
        jsTest {
            dependencies {
                implementation(libs.kotlinTestJs)
            }
        }
        jvmMain {
            dependencies {
                // TODO Uncomment
                // samples(project(":paging:paging-samples"))
            }
        }
        jvmTest {
            dependencies {
                implementation(project(":internal-testutils-ktx"))
                implementation(project(":internal-testutils-paging"))
                implementation(libs.kotlinTestJunit)
                implementation(libs.truth)
            }
        }
    }
}

androidx {
    name = "Android Paging-Common"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.PAGING
    inceptionYear = "2017"
    description = "Android Paging-Common"
    legacyDisableKotlinStrictApiMode = true // TODO: Re-enable this!
    publishPlatforms = ["jvm", "js"]
}

repositories {
    mavenCentral()
}

// Allow usage of Kotlin's @OptIn.
tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
}

rootProject.plugins.withType(Class.forName("org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootPlugin")) {
    rootProject.extensions.getByType(NodeJsRootExtension.class).nodeVersion = "16.0.0"
}
